# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

import os

Import('BuildEnv')
env = BuildEnv.Clone()

def SrcBuildDir(x):
    return '#/' + Dir('.').path + '/../../' + x;

env.Prepend(LIBS=['io', 'base', 'sandesh'])
env.Append(LIBPATH=['.', SrcBuildDir('base'), SrcBuildDir('io')])
env.Append(CPPPATH = env['TOP'])

# libsandeshflow
SandeshGenFiles = env.SandeshGenCpp('flow.sandesh')
SandeshGenSrcs = env.ExtractCpp(SandeshGenFiles)
SandeshGenHdrs = env.ExtractHeader(SandeshGenFiles)
libsandeshflow = env.Library(target = 'sandeshflow', source = [SandeshGenSrcs, 'flow_sandesh_impl.cc'])
env.Install(env['TOP_LIB'], libsandeshflow)
env.Install(env['TOP_INCLUDE'] + '/sandesh/common', SandeshGenHdrs)

#libsandeshvns
SandeshGenFiles = env.SandeshGenCpp('vns.sandesh')
SandeshGenSrcs = env.ExtractCpp(SandeshGenFiles)
SandeshGenHdrs = env.ExtractHeader(SandeshGenFiles)
libsandeshvns = env.Library(target = 'sandeshvns', source = SandeshGenSrcs)
env.Install(env['TOP_LIB'], libsandeshvns)
env.Install(env['TOP_INCLUDE'] + '/sandesh/common', SandeshGenHdrs)

base_path = '#controller/src/sandesh/common/'

# Generate the Python source files
flow_pkg = env.SandeshGenPy('flow.sandesh', base_path + 'sandesh_common/', False)
vns_pkg = env.SandeshGenPy('vns.sandesh', base_path + 'sandesh_common/', False)

sources = ['setup.py']
# there are no sources in this project

sdist_gen = env.Command(
    '/pip/sandesh_common-0.1.dev0-py3-none-any.whl', sources,
    'cd ' + Dir(base_path).abspath + ' && ' + 'python3 setup.py bdist_wheel --dist-dir /pip')
env.Depends(sdist_gen, [flow_pkg, vns_pkg])
env.Alias('install', sdist_gen)

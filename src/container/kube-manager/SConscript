# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

base_path = '#controller/src/container/kube-manager/'

# Sandesh source files.
sandesh_sources = []
sandesh_sources += env.SandeshGenPy('kube_manager.sandesh',
                                    base_path + 'kube_manager/sandesh/', False)
sandesh_sources += env.SandeshGenPy('kube_introspect.sandesh',
                                    base_path + 'kube_manager/sandesh/', False)

# Documentation
if 'install' in BUILD_TARGETS:
    doc_files = []
    doc_files += env.SandeshGenDoc('kube_manager.sandesh')
    doc_files += env.SandeshGenDoc('kube_introspect.sandesh')
    env.Alias('install', env.Install(
        env['INSTALL_MESSAGE_DOC'] + '/contrail-kube-manager/', doc_files))

sources = ['setup.py', 'requirements.txt']
sources += env.AddPythonSources('kube_manager', excludes=['sandesh'])

sdist_gen = env.Command(
    '/pip/kube_manager-0.1.dev0-py3-none-any.whl', sources,
    'cd ' + Dir(base_path).path + ' && ' + 'python3 setup.py bdist_wheel --dist-dir /pip')
env.Depends(sdist_gen, sandesh_sources)
env.Alias('install', sdist_gen)

deps = [
    '/pip/sandesh-0.1.dev0-py3-none-any.whl',
    '/pip/sandesh_common-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_api_client-0.1.dev0-py3-none-any.whl',
    '/pip/cfgm_common-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_api_server-0.1.dev0-py3-none-any.whl',
    '/pip/schema_transformer-0.1.dev0-py3-none-any.whl',
    '/pip/svc_monitor-0.1.dev0-py3-none-any.whl',
]
env.SetupPyTestSuiteWithDeps(sdist_gen, sdist_depends=deps, top_dir=Dir(base_path).abspath)

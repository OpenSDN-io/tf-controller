# -*- mode: python; -*-

import sys

Import('BuildEnv')
OpEnv = BuildEnv.Clone()

base_path = '#controller/src/nodemgr/'

sandesh_sources = []
sandesh_sources += OpEnv.SandeshGenPy(
    '#controller/src/nodemgr/sandesh/database.sandesh', base_path + 'nodemgr/common/sandesh/', False)
sandesh_sources += OpEnv.SandeshGenPy(
    '#src/contrail-common/base/sandesh/nodeinfo.sandesh', base_path + 'nodemgr/common/sandesh/', False)
sandesh_sources += OpEnv.SandeshGenPy(
    '#src/contrail-common/base/sandesh/cpuinfo.sandesh', base_path + 'nodemgr/common/sandesh/nodeinfo/', False)
sandesh_sources += OpEnv.SandeshGenPy(
    '#src/contrail-common/base/sandesh/process_info.sandesh', base_path + 'nodemgr/common/sandesh/nodeinfo/', False)
sandesh_sources += OpEnv.SandeshGenPy(
    '#controller/src/vnsw/agent/uve/loadbalancer.sandesh', base_path + 'nodemgr/common/sandesh/', False)

rel_path = Dir(base_path + 'nodemgr/common').path
def BuildInfoAction(target, source, env):
    env.GenerateBuildInfoPyCode(path=rel_path)
build_info_rules = [OpEnv.Command(target='nodemgr/common/buildinfo.py', source = None, action=BuildInfoAction)]

# Documentation
if 'install' in BUILD_TARGETS:
    doc_files = OpEnv.SandeshGenDoc('sandesh/database.sandesh')
    OpEnv.Alias('install', OpEnv.Install(
        OpEnv['INSTALL_MESSAGE_DOC'] + '/contrail-database/',
        doc_files))

sdist_depends = [sandesh_sources, build_info_rules]

sources = ['setup.py', 'requirements.txt']
sources += OpEnv.AddPythonSources('nodemgr', excludes=['sandesh'])

sdist_gen = OpEnv.Command(
    '/pip/nodemgr-0.1.dev0-py3-none-any.whl', sources,
    'cd ' + Dir(base_path).path + ' && ' + 'python3 setup.py bdist_wheel --dist-dir /pip')
OpEnv.Depends(sdist_gen, sdist_depends)
OpEnv.Alias('install', sdist_gen)

deps = [
    '/pip/sandesh-0.1.dev0-py3-none-any.whl',
    '/pip/sandesh_common-0.1.dev0-py3-none-any.whl',
]
OpEnv.SetupPyTestSuiteWithDeps(sdist_gen, sdist_depends=deps, top_dir=Dir(base_path).abspath)

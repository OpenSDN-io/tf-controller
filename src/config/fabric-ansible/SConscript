# -*- mode: python; -*-

#
# Copyright (c) 2018 Juniper Networks, Inc. All rights reserved.
#
# Placeholder for fabric-ansible Sconscript

import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

base_path = "#controller/src/config/fabric-ansible/"

subdirs=[
    'ansible-playbooks',
]
CfgmEnv.SConscript(dirs=subdirs, exports='CfgmEnv', duplicate = 0)

job_pkg = env.SandeshGenPy('job.sandesh', base_path + 'job_manager/sandesh/', False)

# Documentation
if 'install' in BUILD_TARGETS:
    doc_files = []
    doc_files += env.SandeshGenDoc('job.sandesh')
    doc_files += env['CFGM_DOC_FILES']
    env.Alias('install', env.Install(
        env['INSTALL_MESSAGE_DOC'] + '/contrail-fabric-ansible/', doc_files))

sources = ['setup.py', 'requirements.txt']
sources += env.AddPythonSources('job_manager', excludes=['sandesh'])

sdist_gen = env.Command(
    '/pip/fabric_ansible-0.1.dev0-py3-none-any.whl', sources,
    'cd ' + Dir(base_path).path + ' && ' + 'python3 setup.py bdist_wheel --dist-dir /pip')
env.Depends(sdist_gen, job_pkg)
env.Alias('install', sdist_gen)

deps = [
    '/pip/sandesh-0.1.dev0-py3-none-any.whl',
    '/pip/sandesh_common-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_api_client-0.1.dev0-py3-none-any.whl',
    '/pip/cfgm_common-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_api_server-0.1.dev0-py3-none-any.whl',
]
env.SetupPyTestSuiteWithDeps(sdist_gen, sdist_depends=deps, top_dir=Dir(base_path).abspath)

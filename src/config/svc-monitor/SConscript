#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

base_path = '#controller/src/config/svc-monitor/'

sources_gen = []
sources_gen += env.SandeshGenPy('svc_mon_introspect.sandesh', base_path + 'svc_monitor/sandesh/', False)
sources_gen += env.SandeshGenPy('#controller/src/vnsw/agent/uve/loadbalancer.sandesh', base_path + 'svc_monitor/sandesh/', False)
sources_gen += env.SandeshGenPy('port_tuple.sandesh', base_path + 'svc_monitor/sandesh/', False)
sources_gen += env.SandeshGenPy('snat_agent.sandesh', base_path + 'svc_monitor/sandesh/', False)

if 'install' in BUILD_TARGETS:
    # Documentation
    doc_files = []
    doc_files += env.SandeshGenDoc('svc_mon_introspect.sandesh')
    doc_files += env['CFGM_DOC_FILES']
    doc_files += env.SandeshGenDoc('#controller/src/vnsw/agent/uve/loadbalancer.sandesh')
    doc_files += env['CFGM_DOC_FILES']
    doc_files += env.SandeshGenDoc('port_tuple.sandesh')
    doc_files += env.SandeshGenDoc('snat_agent.sandesh')
    env.Alias('install', env.Install(
        env['INSTALL_MESSAGE_DOC'] + '/contrail-svc-monitor/', doc_files))

sources = ['setup.py', 'requirements.txt']
sources += env.AddPythonSources('svc_monitor', excludes=['sandesh'])

sdist_gen = env.Command(
    '/pip/svc_monitor-0.1.dev0-py3-none-any.whl', sources,
    'cd ' + Dir(base_path).path + ' && ' + 'python3 setup.py bdist_wheel --dist-dir /pip')
env.Depends(sdist_gen, sources_gen)
env.Alias('install', sdist_gen)

deps = [
    '/pip/sandesh-0.1.dev0-py3-none-any.whl',
    '/pip/sandesh_common-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_api_client-0.1.dev0-py3-none-any.whl',
    '/pip/cfgm_common-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_api_server-0.1.dev0-py3-none-any.whl',
]
test = env.SetupPyTestSuiteWithDeps(sdist_gen, sdist_depends=deps, top_dir=Dir(base_path).abspath)
# add alias with underscore - it's hard-coded with it instead of dash
env.Alias('controller/src/config/svc_monitor:test', test)

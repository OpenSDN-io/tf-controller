#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

setup_sources = [
    'setup.py',
    'requirements.txt',
    'test-requirements.txt',
    '.stestr.conf',
    'tox.ini',
    'MANIFEST.in',
]


setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(
        env.Install(Dir('.'), "#controller/src/config/contrail_issu/" + file))

local_sources = [
           '__init__.py',
           'issu_contrail_common.py',
           'issu_contrail_config.py',
           'issu_contrail_post_sync.py',
           'issu_contrail_pre_sync.py',
           'issu_contrail_run_sync.py',
           'issu_contrail_zk_sync.py',
           'test',
          ]

local_sources_rules = []
for file in local_sources:
    local_sources_rules.append(
        env.Install("contrail_issu", "#controller/src/config/contrail_issu/contrail_issu/" + file))

cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_depends = []
sdist_depends.extend(setup_sources_rules)
sdist_depends.extend(local_sources_rules)
pyver = env.GetPyVersion()
sdist_gen = env.Command(
    'dist/contrail_issu-%s.tar.gz' % pyver,
    'setup.py',
    cd_cmd + 'python3 setup.py sdist')

env.Depends(sdist_gen, sdist_depends)
env.Default(sdist_gen)

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None, 'setup.py',
                              cd_cmd + 'python3 setup.py install %s' %
                              env['PYTHON_INSTALL_OPT'])
    env.Depends(install_cmd, sdist_depends)
    env.Alias('install', install_cmd)
    install_root = GetOption('install_root')
    if install_root is None:
        install_root = ''
    env.Alias(
        'install',
        env.Install(
            install_root + '/opt/opensdn/pip/contrail_issu/',
            Entry('#controller/src/config/contrail_issu/requirements.txt')))

test_target = env.SetupPyTestSuite(
    sdist_gen,
    '/config/api-server/dist/contrail-api-server-%s.tar.gz' % pyver,
    '/../noarch/config/svc-monitor/dist/svc_monitor-%s.tar.gz' % pyver,
    '/config/schema-transformer/dist/schema_transformer-%s.tar.gz' % pyver,
    use_tox=True)

env.Alias('controller/src/config/contrail_issu:test', test_target)

# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

Import('CfgmEnv')
env = CfgmEnv.Clone()

base_path = '#controller/src/config/schema-transformer/'

sandesh_sources = env.SandeshGenPy('st_introspect.sandesh', base_path + 'schema_transformer/sandesh/', False)

if 'install' in BUILD_TARGETS:
    # Documentation
    doc_files = []
    doc_files += env.SandeshGenDoc('st_introspect.sandesh')
    doc_files += env['CFGM_DOC_FILES']
    env.Alias('install', env.Install(
        env['INSTALL_MESSAGE_DOC'] + '/contrail-schema/', doc_files))

sdist_gen = env.Command(
    '/pip/schema_transformer-0.1.dev0-py3-none-any.whl', 'setup.py',
    'cd ' + Dir(base_path).path + ' && ' + 'python3 setup.py bdist_wheel --dist-dir /pip')
env.Depends(sdist_gen, sandesh_sources)
env.Alias('install', sdist_gen)

deps = [
    '/pip/sandesh-0.1.dev0-py3-none-any.whl',
    '/pip/sandesh_common-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_api_client-0.1.dev0-py3-none-any.whl',
    '/pip/cfgm_common-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_api_server-0.1.dev0-py3-none-any.whl',
    '/pip/svc_monitor-0.1.dev0-py3-none-any.whl',
]
env.SetupPyTestSuiteWithDeps(sdist_gen, sdist_depends=deps, top_dir=Dir(base_path).abspath)

#
# Copyright (c) 2013 Contrail Systems. All rights reserved.
#

import os
import glob

Import('CfgmEnv')
env = CfgmEnv.Clone()

source_files = [
                'requirements.txt',
                'setup.py',
                '__init__.py',
                'vnc_openstack/__init__.py',
                'vnc_openstack/context.py',
                'vnc_openstack/neutron_plugin_db.py',
                'vnc_openstack/neutron_plugin_interface.py',
                'vnc_openstack/utils.py'
]

source_rules = []
for file in source_files:
    if len(file.split('/')) > 1:
        idir = '/'.join(file.split('/')[:-1])
    else:
        idir = ''
    source_rules.append(
        env.Install(idir, "#/controller/src/config/vnc_openstack/" + file))

test_sources = [
           'requirements.txt',
           'test-requirements.txt',
           '.stestr.conf',
           'tox.ini',
           '.coveragerc',
           'vnc_openstack/tests',
           'MANIFEST.in',
]

test_sources_rules = []
for file in test_sources:
    test_sources_rules.append(
       env.Install(Dir('.'), "#controller/src/config/vnc_openstack/" + file))

cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_depends = []
sdist_depends.extend(source_rules)
sdist_depends.extend(test_sources_rules)
sdist_gen = env.Command('dist', 'setup.py', cd_cmd + 'python3 setup.py sdist')

env.Depends(sdist_gen, sdist_depends)
env.Default(sdist_gen)

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None, 'setup.py',
                              cd_cmd + 'python3 setup.py install %s' %
                              env['PYTHON_INSTALL_OPT'])
    env.Depends(install_cmd, sdist_depends)
    env.Alias('install', install_cmd)
    install_root = GetOption('install_root')
    if install_root is None:
        install_root = ''
    env.Alias(
        'install',
        env.Install(
            install_root + '/opt/opensdn/pip/vnc_openstack/',
            Entry('#controller/src/config/vnc_openstack/requirements.txt')))

test_target = env.SetupPyTestSuite(
    sdist_gen,
    '/config/api-server/dist/contrail-api-server-%s.tar.gz' % env.GetPyVersion(),
    use_tox=True)

# This add'l alias is to allow retry of UT in CI
env.Alias('controller/src/config/vnc_openstack/vnc_openstack:test', test_target)

# Local Variables:
# mode: python
# End:

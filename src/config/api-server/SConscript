#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

base_path = '#controller/src/config/api-server/'

sandesh_trace_pkg = env.SandeshGenPy(
    'traces.sandesh', base_path + 'vnc_cfg_api_server/sandesh/', False)

# Documentation
if 'install' in BUILD_TARGETS:
    doc_files = []
    doc_files += env.SandeshGenDoc('traces.sandesh')
    doc_files += env['CFGM_DOC_FILES']
    env.Alias('install', env.Install(
        env['INSTALL_MESSAGE_DOC'] + '/contrail-api/', doc_files))

sources = ['setup.py', 'requirements.txt']
sources += env.AddPythonSources('vnc_cfg_api_server', excludes=['sandesh'])

sdist_gen = env.Command(
    '/pip/contrail_api_server-0.1.dev0-py3-none-any.whl', sources,
    'cd ' + Dir(base_path).path + ' && ' + 'python3 setup.py bdist_wheel --dist-dir /pip')
env.Depends(sdist_gen, sandesh_trace_pkg)
env.Alias('install', sdist_gen)

deps = [
    '/pip/sandesh-0.1.dev0-py3-none-any.whl',
    '/pip/sandesh_common-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_api_client-0.1.dev0-py3-none-any.whl',
    '/pip/cfgm_common-0.1.dev0-py3-none-any.whl',
]
env.SetupPyTestSuiteWithDeps(sdist_gen, sdist_depends=deps, top_dir=Dir(base_path).abspath)
